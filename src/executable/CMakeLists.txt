
## MacOS specific assets ##
set(MAC_PROJECT_ICON )
if (${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
    # The MACOSX_BUNDLE_ICON_FILE variable is added to the Info.plist
    # generated by CMake. This variable contains the .icns file name,
    # without the path.
    set(MACOSX_BUNDLE_ICON_FILE icon)

    # And the following tells CMake where to find and install the file itself.
    set(MAC_PROJECT_ICON "${PROJECT_SOURCE_DIR}/assets/icons/icon.icns")
    set_source_files_properties(${MAC_PROJECT_ICON} PROPERTIES
            MACOSX_PACKAGE_LOCATION "Resources")
endif()


## Windows specific assets ##
set(WIN_PROJECT_ICONS )
if (${CMAKE_SYSTEM_NAME} MATCHES "Windows")
    set(WIN_PROJECT_ICONS
            ${PROJECT_SOURCE_DIR}/assets/icons/windows_icon.rc
            #${PROJECT_SOURCE_DIR}/assets/icons/windows_icon.qrc # todo check if needed
            )
endif()


qt_add_executable(${PROJECT_NAME}
        MANUAL_FINALIZATION
        main.cpp
        ${TS_FILES}
        ${MAC_PROJECT_ICON}
        ${WIN_PROJECT_ICONS}
)

#qt6_create_translation(QM_FILES ${CMAKE_SOURCE_DIR} ${TS_FILES})
qt_add_translations(${PROJECT_NAME} TS_FILES ${TS_FILES})

target_link_libraries(${PROJECT_NAME} PRIVATE app)

# todo my.example.com
set_target_properties(${PROJECT_NAME} PROPERTIES
        MACOSX_BUNDLE_GUI_IDENTIFIER my.example.com
        MACOSX_BUNDLE_BUNDLE_VERSION ${PROJECT_VERSION}
        MACOSX_BUNDLE_SHORT_VERSION_STRING ${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}
        MACOSX_BUNDLE TRUE
        WIN32_EXECUTABLE TRUE
)

qt_finalize_executable(${PROJECT_NAME})


if (LINUX)
    if (FLATPAK_BUILD)

        set(APPID "org.OverEngineer.CuteTales")

        # Need a install target in the makefile for flatpak-builder
        install(TARGETS ${PROJECT_NAME} DESTINATION ${CMAKE_INSTALL_BINDIR})

        install(FILES ${PROJECT_SOURCE_DIR}/assets/icons/64-apps-org.OverEngineer.CuteTales.png DESTINATION /app/share/icons/hicolor/64x64/apps RENAME ${APPID}.png)
        install(FILES ${PROJECT_SOURCE_DIR}/assets/icons/128-apps-org.OverEngineer.CuteTales.png DESTINATION /app/share/icons/hicolor/128x128/apps RENAME ${APPID}.png)

        install(PROGRAMS ${PROJECT_SOURCE_DIR}/deploy/linux/org.OverEngineer.CuteTales.desktop DESTINATION /app/share/applications RENAME ${APPID}.desktop)
        install(FILES ${PROJECT_SOURCE_DIR}/deploy/linux/flatpak/org.OverEngineer.CuteTales.appdata.xml DESTINATION /app/share/metainfo RENAME ${APPID}.appdata.xml)
    endif()
endif()
